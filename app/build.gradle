apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def localProperties = new Properties()
localProperties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'app'
    productFlavors {
        mobileLiveness {
            applicationId "pl.edu.agh.bioauthdemo.mobileliveness"
            resValue('string', 'app_name', 'BioAuthDemo (Mobile Liveness)')
            buildConfigField('String', 'LIVENESS_MODE', '"MOBILE"')
            buildConfigField('String', 'APP_ID', localProperties.getProperty('mobileLiveness.appId'))
            buildConfigField('String', 'APP_SECRET', localProperties.getProperty('mobileLiveness.appSecret'))
            versionNameSuffix('-mobileLiveness')
        }
        serverLiveness {
            applicationId "pl.edu.agh.bioauthdemo.serverliveness"
            resValue('string', 'app_name', 'BioAuthDemo (Server Liveness)')
            buildConfigField('String', 'LIVENESS_MODE', '"SERVER"')
            buildConfigField('String', 'APP_ID', localProperties.getProperty('serverLiveness.appId'))
            buildConfigField('String', 'APP_SECRET', localProperties.getProperty('serverLiveness.appSecret'))
            versionNameSuffix('-serverLiveness')
        }
        mlLiveness {
            applicationId "pl.edu.agh.bioauthdemo.mlliveness"
            resValue('string', 'app_name', 'BioAuthDemo (ML Liveness)')
            buildConfigField('String', 'LIVENESS_MODE', '"MACHINE_LEARNING"')
            buildConfigField('String', 'APP_ID', localProperties.getProperty('mlLiveness.appId'))
            buildConfigField('String', 'APP_SECRET', localProperties.getProperty('mlLiveness.appSecret'))
            versionNameSuffix('-mlLiveness')
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':bioauth')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
